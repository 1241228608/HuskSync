plugins {
    id 'java'
    id 'com.github.johnrengelman.shadow' version '7.1.0'
}

repositories {
    mavenCentral()
    maven { url 'https://hub.spigotmc.org/nexus/content/repositories/snapshots/' }
    maven { url 'https://repo.minebench.de/' }
    maven { url 'https://repo.codemc.org/repository/maven-public' }
    maven { url 'https://jitpack.io' }
}

dependencies {
    api project(':common')
    implementation 'de.themoep:minedown:1.7.1-SNAPSHOT'
    implementation 'org.bstats:bstats-bukkit:2.2.1'
    implementation 'com.zaxxer:HikariCP:5.0.0'
    compileOnly 'org.spigotmc:spigot-api:1.16.5-R0.1-SNAPSHOT'
}

processResources {
    def props = [version: project.version]
    inputs.properties props
    expand props
    filteringCharset 'UTF-8'
    filesMatching('bungee.yml') {
        expand props
    }
}

shadowJar {
    relocate 'org.bstats', 'me.william278.shaded.org.bukkit.bstats'
    relocate 'de.themoep.minedown', 'me.william278.shaded.de.themeop.minedown'
    dependencies {
        include(dependency(':common'))
    }
}

artifacts {
    archives shadowJar
}

tasks.build.dependsOn tasks.shadowJar

task createFinalJar(type: Copy) {
    dependsOn(tasks.build)
    from file("build/libs/bukkit-${project.version}-all.jar")
    into file("../build-output-final")
    rename 'bukkit-', 'CrossServerSync-'
    rename '-all', ''
}

task prepareKotlinBuildScriptModel {}